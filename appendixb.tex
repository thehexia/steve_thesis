\chapter{STEVE PROGRAMS} \label{ap:steve_programs}

\begin{lstlisting}
///////////////////////////////////////
//  Wire
//////////////////////////////////////

layout ethernet
{
  dst : uint(48);
  src : uint(48);
  type : uint(16);
}

Port p1;
Port p2;

decoder start eth_d(ethernet)
{
	// Port variables, p1 and p2, "remember" ports.

	// Whenever a packet is handled, check if p1 and p2 are set.
	// If neither are, set p1 equal to the ingress port.
	if (p1 == 0 && p2 == 0)
		p1 = in_port;
	// If p1 is set, and p2 isn't, set p2 to the ingress port.
	else if (p1 != 0 && p2 == 0)
		p2 = in_port;

	// Now we decide which packet to forward to.
	// If the ingress port is p1, and p2 is set, forward to p2.
	if (in_port == p1 && p2 != 0)
		output p2;
	// If the ingress port is p2, and p1 is set, forward to p1.
	if (in_port == p2 && p1 != 0)
		output p1;

	// If both are not set yet, do nothing and implicitly drop.
}
\end{lstlisting}


\begin{lstlisting}
///////////////////////////////////////
//  MAC Learning Switch
//////////////////////////////////////

layout ethernet
{
	dst : uint(48);
	src : uint(48);
	type : uint(16);
}


decoder start eth_d(ethernet)
{
	extract ethernet.dst;
	extract ethernet.src;
  goto learn;
}

// This event will do the "learning" through flow inserts.
event learn_mac
	requires(ethernet.src) // It requires the src MAC
{
	// First we insert the src of the packet
	// into the learn table so we don't keep
	// trying to learn something we already have.
	insert into learn
	{ ethernet.src } -> [timeout = 60] { goto forward; };

	// Next we insert the src of the current packet
	// into the forward table.
	//
	// The forward table matches on the dst field of a packet.
	// What we are doing is saying any packet whose dst is equal
	// to this packet's src is forwarded to this packet's
	// ingress port.
	insert into forward
	{ ethernet.src } ->
	[timeout = 60, egress = in_port]
	{
		// We set the egress property to the current
		// packet's in_port. Future packets will be forwarded to
		// the current packet's ingress port.
		output egress;
	};
}


exact_table learn(ethernet.src)
{
  miss ->
  {
	raise learn_mac;
    goto forward;
  }
}


exact_table forward(ethernet.dst)
{
  miss ->
  {
    output flood;
  }
}
\end{lstlisting}



\begin{lstlisting}
///////////////////////////////////////
//  IPV4 Learning Switch
//////////////////////////////////////

layout ethernet
{
  src : uint(48);
  dst : uint(48);
  type : uint(16);
}

layout ipv4
{
  version_ihl : uint(8);
  dscp_ecn    : uint(8);
  len         : uint(16);
  id          : uint(16);
  fragment    : uint(16);
  ttl         : uint(8);
  protocol    : uint(8);
  checksum    : uint(16);
  src         : uint(32);
  dst         : uint(32);
}


decoder start eth_d(ethernet)
{
  extract ethernet.type;
  if (ethernet.type >= 0x600)
      // The next header is IPv4 if type field is 0x800.
      match (ethernet.type) {
        case 0x800: decode ipv4_d;
      }
  // If its not IPv4, processing ends and the packet is
  // implicitly dropped.
}

decoder ipv4_d(ipv4)
{
  // We actually need all fields to confirm the checksum.
  extract ipv4.version_ihl;
  extract ipv4.dscp_ecn;
  extract ipv4.len;
  extract ipv4.id;
  extract ipv4.fragment;
  extract ipv4.ttl; // Use time-to-live to decrement
  extract ipv4.protocol;
  extract ipv4.checksum;
  extract ipv4.src;
  extract ipv4.dst;

  // Calculate a checksum.
  var checksum : uint(16) =
     ipv4_checksum(ipv4.version_ihl, ipv4.dscp_ecn, 
     				ipv4.len, ipv4.id, ipv4.fragment, ipv4.ttl, 
     				ipv4.protocol, ipv4.src, ipv4.dst);

  // Check the checksum against the header's checksum.
  if (checksum != ipv4.checksum)
    drop;

  // Drop time-to-live expired packets.
  if (ipv4.ttl == 0)
    drop;

  set ipv4.ttl = ipv4.ttl - 1; // Decrement ttl.

  // We've changed the ttl, we must set a new checksum.
  set ipv4.checksum =
    ipv4_checksum(ipv4.version_ihl, ipv4.dscp_ecn, 
    				ipv4.len, ipv4.id, ipv4.fragment, 
    				ipv4.ttl, ipv4.protocol, ipv4.src, ipv4.dst);

  // Proceed to the learn table after advancing by ihl
  goto learn advance (ipv4.version_ihl & 0x0f) * 4;
}


// This event handles the actual "learning."
event learn_ip
  requires(ipv4.src) // It will learn src IP addresses.
{
  // This first entry prevents the same address from causing
  // this event twice. Sends the packet straight to routing.
  insert into learn
  { ipv4.src } -> [timeout = 30] { goto route; };

  // This establishes the IP address to port mapping.
  // Any packet whose dst address matches the current packet's
  // src address will be forwarded to the current packet's
  // ingress port.
  insert into route
  { ipv4.src } ->
  [timeout = 30, egress = in_port]
  {
    output egress;
  };
}


exact_table learn(ipv4.src)
{
  miss ->
  {
    raise learn_ip;
    goto route;
  }
}


// This ultimately decides where to forward packets based on
// their destination IP.
exact_table route(ipv4.dst)
  requires(ipv4.version_ihl, ipv4.dscp_ecn, ipv4.len, ipv4.id,
           ipv4.fragment, ipv4.ttl, ipv4.protocol, ipv4.src)
{
  miss ->
  {
    output flood; // Flood on all unlearned addresses.
  }
}


def ipv4_checksum(vihl : uint(8), dscp_ecn : uint(8), 
					len : uint(16),	id : uint(16), frag : uint(16), 
					ttl : uint(8),  proto : uint(8), src : uint(32), 
					dst : uint(32) ) -> uint(16)
{
  // Merge fields into 16-bit values.
  var merge1 : uint(16) = vihl << 8;
  merge1 = merge1 | dscp_ecn;

  var merge2 : uint(16) = ttl << 8;
  merge2 = merge2 | proto;

  // Split fields int 16-bit values.
  var split_src1 : uint(16) = src >> 16;
  // Get the lower 16 bits.
  var split_src2 : uint(16) = src & 0x0000_ffff;

  // Get the upper 16 bits.
  var split_dst1 : uint(16) = dst >> 16;
  // Get the lower 16 bits.
  var split_dst2 : uint(16) = dst & 0x0000_ffff;

  // Accumulated sum.
  var acc : uint(32) = 0;
  acc = acc + merge1 + len + id + frag + merge2 +
        split_src1 + split_src2 + split_dst1 + split_dst2;

  // Perform the 1's complement sum wraparound.
  var acc1 : uint(16) = acc >> 16;
  var acc2 : uint(16) = acc & 0x0000_ffff;

  acc2 = acc1 + acc2;
  acc2 = acc2 ^ 0xffff_ffff;
  return acc2;
}
\end{lstlisting}


\begin{lstlisting}
///////////////////////////////////////
//  Firewall
//////////////////////////////////////

layout eth
{
  dst : uint(48);
  src : uint(48);
  type : uint(16);
}


layout ipv4
{
  version_ihl : uint(8);
  dscp_ecn    : uint(8);
  len         : uint(16);
  id          : uint(16);
  fragment    : uint(16);
  ttl         : uint(8);
  protocol    : uint(8);
  checksum    : uint(16);
  src         : uint(32);
  dst         : uint(32);
  // Ignore options.
}


layout udp
{
  src      : uint(16);
  dst      : uint(16);
  len      : uint(16);
  checksum : uint(16);
}


layout tcp
{
  src : uint(16);
  dst : uint(16);
  ack : uint(32);
  seq : uint(32);
  control : uint(16);
  window : uint(16);
  checksum : uint(16);
  urgent_ptr : uint(16);
  // Ignore options.
}


decoder start eth_d(eth)
{
  extract eth.type;

  // Check for IPv4. Ignore IPv6 for now.
  match(eth.type)
  {
    case 0x800: decode ipv4_d;
  }
  // Don't worry about other kinds of packets.
}


decoder ipv4_d(ipv4)
{
  // We actually need all fields to confirm the checksum.
  extract ipv4.version_ihl; // Use this to get header length.
  extract ipv4.dscp_ecn;
  extract ipv4.len;
  extract ipv4.id;
  extract ipv4.fragment;
  extract ipv4.ttl;       // Use time-to-live to decrement
  extract ipv4.protocol;
  extract ipv4.checksum;
  extract ipv4.src;
  extract ipv4.dst;

  // Calculate a checksum.
  var checksum : uint(16) =
     ipv4_checksum(ipv4.version_ihl, ipv4.dscp_ecn, ipv4.len, 
					ipv4.id, ipv4.fragment, ipv4.ttl, ipv4.protocol, 
					ipv4.src, ipv4.dst);

  // Check the checksum against the header's checksum.
  if (checksum != ipv4.checksum)
    drop;

  // Drop time-to-live expired packets.
  if (ipv4.ttl == 0)
    drop;

  set ipv4.ttl = ipv4.ttl - 1; // Decrement ttl.

  // We've changed the ttl, we must set a new checksum.
  set ipv4.checksum =
    ipv4_checksum(ipv4.version_ihl, ipv4.dscp_ecn, 
    				ipv4.len, ipv4.id, ipv4.fragment, 
    				ipv4.ttl, ipv4.protocol, ipv4.src, ipv4.dst);

  var hdr_len : uint(8) =  (ipv4.version_ihl & 0x0f) * 4;

  // Only care about udp and tcp requests,
  match (ipv4.protocol) {
    case 0x06: decode tcp_d advance hdr_len;
    case 0x11: decode udp_d advance hdr_len;
  }
}

decoder udp_d(udp)
{
  extract udp.dst;
  goto udp_filter;
}


decoder tcp_d(tcp)
{
  extract tcp.dst;
  goto tcp_filter;
}


// Only route if the port# is 80 or 443
// Log the ip src address of all blocked and all allowed.
exact_table udp_filter(udp.dst)
  requires(ipv4.src)
{
  { 80 } ->
  {
    goto learn;
  }

  { 443 } ->
  {
    goto learn;
  }

  miss ->
  {
    drop;
  }
}


// Only route if the port# is 80 or 443
// Log the IP address of all blocked and all allowed.
exact_table tcp_filter(tcp.dst)
  requires(ipv4.src)
{
  { 80 } ->
  {
    goto learn;
  }

  { 443 } ->
  {
    goto learn;
  }

  miss ->
  {
    drop;
  }
}


// This event handles the actual "learning."
event learn_ip
  requires(ipv4.src) // It will learn src IP addresses.
{
  // This first entry prevents the same address from causing
  // this event twice. Sends the packet straight to routing.
  insert into learn
  { ipv4.src } -> [timeout = 30] { goto route; };

  // This establishes the IP address to port mapping.
  // Any packet whose dst address matches the current packet's
  // src address will be forwarded to the current packet's
  // ingress port.
  insert into route
  { ipv4.src } ->
  [timeout = 30, egress = in_port]
  {
    output egress;
  };
}


// Learning table.
exact_table learn(ipv4.src)
{
  miss ->
  {
    raise learn_ip;
    goto route;
  }
}


// This ultimately decides where to forward packets based on
// their destination IP.
exact_table route(ipv4.dst)
  requires(ipv4.ttl)
{
  miss -> {
    output flood; // Flood on all unlearned addresses.
  }
}


def ipv4_checksum(vihl : uint(8), dscp_ecn : uint(8), 
					len : uint(16),	id : uint(16), frag : uint(16), 
					ttl : uint(8),  proto : uint(8), src : uint(32), 
					dst : uint(32) ) -> uint(16)
{
  // Merge fields into 16-bit values.
  var merge1 : uint(16) = vihl << 8;
  merge1 = merge1 | dscp_ecn;

  var merge2 : uint(16) = ttl << 8;
  merge2 = merge2 | proto;

  // Split fields int 16-bit values.
  var split_src1 : uint(16) = src >> 16;
  // Get the lower 16 bits.
  var split_src2 : uint(16) = src & 0x0000_ffff;

  // Get the upper 16 bits.
  var split_dst1 : uint(16) = dst >> 16;
  // Get the lower 16 bits.
  var split_dst2 : uint(16) = dst & 0x0000_ffff;

  // Accumulated sum.
  var acc : uint(32) = 0;
  acc = acc + merge1 + len + id + frag + merge2 +
        split_src1 + split_src2 + split_dst1 + split_dst2;

  // Perform the 1's complement sum wraparound.
  var acc1 : uint(16) = acc >> 16;
  var acc2 : uint(16) = acc & 0x0000_ffff;

  acc2 = acc1 + acc2;
  acc2 = acc2 ^ 0xffff_ffff;
  return acc2;
}
\end{lstlisting}